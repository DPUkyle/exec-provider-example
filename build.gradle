import org.gradle.api.provider.ValueSourceParameters

import java.nio.charset.StandardCharsets

abstract class FooVersionPrinter extends DefaultTask {
    @Input
    abstract Property<String> getResult()

    @TaskAction
    void printVersion() {
        logger.quiet(result.getOrElse('Not installed'))
    }
}

abstract class FooVersionValueSource implements ValueSource<String, ValueSourceParameters.None> {
    @Inject
    abstract ExecOperations getExecOperations()

    @Override
    String obtain() {
        ByteArrayOutputStream output = new ByteArrayOutputStream()
        try {
            execOperations.exec {
                it.commandLine "foo", "--version"
                it.standardOutput = output
                it.ignoreExitValue = true
            }
        } catch (Exception e) {
            return 'Not installed'
        }
        return new String(output.toByteArray(), StandardCharsets.UTF_8)
    }
}

tasks.register("printFooVersion", FooVersionPrinter) {
    it.result = providers.of(FooVersionValueSource) {}
}